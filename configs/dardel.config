// Adapted from https://github.com/nf-core/configs/blob/master/conf/pdc_kth.config

params {
    // SNIC compute allocation id
    project = ''
    // Just FYI.
    // max_memory = 1790.GB
    // max_cpus = 256
    // max_time = 7.d
}

def clusterOptionsCreator = { mem, time, cpus ->
    List clusterOpts = [ "-A $params.project ${params.clusterOptions ?: ''}" ]
    if (time < 1.d && mem <= 222.GB && cpus < 256) {
        clusterOpts << '-p shared'
    } else if (time < 1.d) {
        // Shortish
        if (mem > 222.GB) {
            clusterOpts << '-p memory,main'
        } else {
            clusterOpts << '-p main'
        }
    } else {
        // Not shortish
        if (mem > 222.GB) {
            clusterOpts << '-p memory'
        } else {
            clusterOpts << '-p long'
        }
    }
    if (!mem || mem < 6.GB) {
        // Impose minimum memory if request is below
        clusterOpts << '--mem=6G'
    }
    return clusterOpts.join(' ')
}

process {
    executor       = 'slurm'
    clusterOptions = { clusterOptionsCreator(task.memory, task.time, task.cpus) }
    // There is no node local storage, but there is a scratch space.
    scratch        = '$PDC_TMP'
}
singularity {
    enabled        = true
    envWhitelist   = 'PDC_TMP'
}
