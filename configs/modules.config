stage {
    inspect    = '01_read_inspection'
    preprocess = '02_read_preprocessing'
    assembly   = '03_assembly'
    screen     = '04_contamination_screen'
    purge      = '05_duplicate_purging'
    polish     = '06_error_polishing'
    scaffold   = '07_scaffolding'
    curate     = '08_rapid_curation'
    alignRNA   = '09_RNA_alignment'
    report     = '10_report'
}

process {

    // PREPARE INPUT
    withName: 'UNTAR_TAXONOMY' {
        storeDir   = { params.cache }
    }
    withName: 'GOAT_TAXONSEARCH' {
        ext.args   = "--lineage --variables odb10_lineage,haploid_number,genome_size,ploidy"
        publishDir = [
            path: { "$params.outdir/$stage.inspect/goat" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }
    withName: 'PREPARE_INPUT:SAMTOOLS_FASTA' {
        tag        = { "$meta.id:$bam.baseName" }
        ext.prefix = { bam.baseName }
    }
    withName: 'MERGE_PACBIO' {
        ext.prefix = { "${meta.id}_mergedpb.fasta.gz" }
    }

    // BUILD FASTK DATABASES
    withName: 'FASTK_FASTK' {
        scratch    = false  // !Disable scratch to get around bug https://github.com/nextflow-io/nextflow/issues/2983
        ext.args   = { "-t1 -k${params.fastk?.kmer_size?:31}" }
    }
    withName: 'FASTK_MERGE' {
        scratch    = false  // !Disable scratch to get around bug https://github.com/nextflow-io/nextflow/issues/2983
        ext.prefix = { "${meta.id}_merged" }
    }
    withName: 'BUILD_FASTK_HIFI_DATABASE:FASTK_FASTK' {
        ext.prefix = { "${meta.id}_${reads instanceof List ? reads[0].baseName : reads.baseName}_hifi" }
    }
    withName: 'BUILD_FASTK_HIC_DATABASE:FASTK_FASTK' {
        ext.prefix = { "${meta.id}_${reads instanceof List ? reads[0].baseName : reads.baseName}_hic" }
    }
    withName: 'BUILD_MERYL_HIFI_DATABASE:MERYL_UNIONSUM' {
        ext.prefix = { "${meta.id}_hifi" }
    }
    withName: 'BUILD_MERYL_HIC_DATABASE:MERYL_UNIONSUM' {
        ext.prefix = { "${meta.id}_hic" }
    }

    // GENOME PROPERTIES
    withName: 'FASTK_HISTEX' {
        ext.args   = "-G"
        ext.prefix = { "${meta.id}_histex" }
    }
    withName: 'GENESCOPEFK' {
        ext.args   = {
            [
                "--kmer_length ${params.genescopefk?.kmer_size?:31}",
                "--ploidy ${meta.sample.ploidy?:1}"
            ].join(' ')
        }
        publishDir = [
            path: { "$params.outdir/$stage.inspect/genescopefk" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }
    withName: 'MERQURYFK_PLOIDYPLOT' {
        ext.prefix = { "${meta.id}_ploidyplot" }
        publishDir = [
            path: { "$params.outdir/$stage.inspect/ploidyplot" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }
    withName: 'MERQURYFK_KATGC' {
        ext.prefix = { "${meta.id}_katgc" }
        publishDir = [
            path: { "$params.outdir/$stage.inspect/katgc" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // COMPARE_LIBRARIES
    withName: 'MERQURYFK_KATCOMP' {
        publishDir = [
            path: { "$params.outdir/$stage.inspect/kat_comp" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // SCREEN READS
    withName: 'MASH_SCREEN' {
        ext.args   = '-w'
        ext.prefix = { "${meta.id}_${query.baseName}" }
        publishDir = [
            path: { "$params.outdir/$stage.inspect/mash_screen/screens" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }
    withName: 'MASH_FILTER' {
        ext.args   = "-F '[\\t/]' '\$1 > 0.9 && \$2 > 100'"
        publishDir = [
            path: { "$params.outdir/$stage.inspect/mash_screen" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // ASSEMBLE_HIFI
    withName: 'HIFIASM' {
        tag        = { meta.assembly.build }
        ext.args   = { meta.settings.hifiasm.args }
        ext.prefix = { "$meta.assembly.build" }
        publishDir = [
            path: { "$params.outdir/$stage.assembly/$meta.assembly.build" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }
    withName: 'GFASTATS' {
        tag        = { meta.assembly.build }
        ext.prefix = { assembly.baseName }
        ext.args   = "--nstar-report"
        publishDir = [
            path: { "$params.outdir/$stage.assembly/$meta.assembly.build" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }
    withName: 'GFATOOLS_GFA2FA' {
        tag        = { meta.assembly.build }
        ext.prefix = { gfa.baseName }
        publishDir = [
            path: { "$params.outdir/$stage.assembly/$meta.assembly.build" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // MITOHIFI
    withName: 'MITOHIFI_FINDMITOREFERENCE' {
        publishDir = [
            path: { "$params.outdir/$stage.assembly/mitochondria/reference" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }
    withName: 'MITOHIFI_MITOHIFI' {
        errorStrategy = 'ignore' // TODO Build strategy with deal with failure
        tag        = { meta.assembly.build }
        ext.prefix = { "${meta.id}-${meta.assembly.build}" }
        publishDir = [
            path: { "$params.outdir/$stage.assembly/mitochondria/$meta.assembly.build" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // COMPARE_ASSEMBLIES
    withName: 'QUAST' {
        cpus       = { Math.min(6, consensus instanceof List ? consensus.size() : 1 ) }
        publishDir = [
            path: { "$params.outdir/$stage.assembly/quast" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // EVALUATE_RAW_ASSEMBLY
    withName: 'EVALUATE_RAW_ASSEMBLY:MERQURYFK_MERQURYFK' {
        tag        = { meta.assembly.build }
        ext.prefix = { meta.assembly.build }
        publishDir = [
            path: { "$params.outdir/$stage.assembly/merquryfk/$meta.assembly.build" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }
    withName: 'EVALUATE_RAW_ASSEMBLY:MERQURY' {
        tag        = { meta.assembly.build }
        ext.prefix = { meta.assembly.build }
        publishDir = [
            path: { "$params.outdir/$stage.assembly/merqury/$meta.assembly.build" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }
    withName: 'EVALUATE_RAW_ASSEMBLY:BUSCO' {
        tag        = { "${meta.assembly.build}-${lineage}" }
        ext.prefix = { "${meta.assembly.build}-${lineage}" }
        ext.args   = '--tar'
        publishDir = [
            path: { "$params.outdir/$stage.assembly/busco/$meta.assembly.build" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
        time       = 4.d
    }

    // FCS
    withName: 'FCSGX_FETCHDB' {
        storeDir = { params.cache }
        time     = 2.d
    }
    withName: 'FCSGX_RUNGX' {
        tag        = { "${meta.assembly.build}-hap${meta.haplotype}" }
        ext.prefix = { "${meta.assembly.build}-hap${meta.haplotype}" }
        publishDir = [
            path: { "$params.outdir/$stage.screen/fcsgx" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
        cpus       = 20
        memory     = 1024.GB
        time       = 1.h
        clusterOptions = { "-A $params.project -C mem1TB" }
    }
    withName: 'FCSGX_CLEAN' {
        tag        = { "${meta.assembly.build}-hap${meta.haplotype}" }
        ext.prefix = { "${meta.assembly.build}-hap${meta.haplotype}" }
        publishDir = [
            path: { "$params.outdir/$stage.screen/fcsgx" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // PURGE_DUPLICATES
    withName: 'MINIMAP2_ALIGN_READS' {
        tag        = { meta.assembly.build }
        ext.args   = "-x map-hifi"
        ext.prefix = { "${meta.id}_" + (reads.getName().endsWith(".gz") ? reads.getBaseName(2) : reads.getBaseName()) } 
    }
    withName: 'MINIMAP2_ALIGN_ASSEMBLY_.*' {
        tag        = { meta.assembly.build }
        ext.args   = "-x asm5 -DP"
    }
    withName: 'PURGEDUPS_PURGEDUPS' {
        ext.args   = "-2"
    }
    withName: 'PURGEDUPS_CALCUTS' {
        // kmer coverage is very close to read coverage w.r.t long read sequencing.
        // kmer coverage is used as a proxy for read coverage here.
        // kmer coverage is extracted from the GenomeScope model when available.
        ext.args   = { meta.kmercov ? "-m ${meta.kmercov * 1.5} -u ${meta.kmercov * 3}" : "" }
    }
    withName: '(PURGEDUPS|MINIMAP2)_.*_PRIMARY' {
        ext.prefix = { "${meta.id}_${meta.assembly.build}_hap0" }
    }
    withName: '(PURGEDUPS|MINIMAP2)_.*_ALTERNATE' {
        ext.prefix = { "${meta.id}_${meta.assembly.build}_hap1" }
    }
    withName: 'PURGE_DUPLICATES:SEQKIT_SEQ' {
        ext.prefix = { "${fastx.baseName}_fold" }
        ext.args = "-w 100"
    }
    withName: 'PURGEDUPS_.*' {
        tag        = { meta.assembly.build }
        publishDir = [
            path: { "$params.outdir/$stage.purge/purge_dups" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // EVALUATE_PURGED_ASSEMBLY
    withName: 'EVALUATE_PURGED_ASSEMBLY:MERQURYFK_MERQURYFK' {
        tag        = { meta.assembly.build }
        ext.prefix = { meta.assembly.build }
        publishDir = [
            path: { "$params.outdir/$stage.purge/merquryfk/$meta.assembly.build" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }
    withName: 'EVALUATE_PURGED_ASSEMBLY:MERQURY' {
        tag        = { meta.assembly.build }
        ext.prefix = { meta.assembly.build }
        publishDir = [
            path: { "$params.outdir/$stage.purge/merqury/$meta.assembly.build" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }
    withName: 'EVALUATE_PURGED_ASSEMBLY:BUSCO' {
        tag        = { "${meta.assembly.build}-${lineage}" }
        ext.prefix = { "${meta.assembly.build}-${lineage}" }
        ext.args   = '--tar'
        publishDir = [
            path: { "$params.outdir/$stage.purge/busco/$meta.assembly.build" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
        time       = 4.d
    }

    withName: 'STAR_ALIGN' {
        tag        = { meta.assembly.build }
        ext.args   = '--readFilesCommand zcat'
        publishDir = [
            path: { "$params.outdir/$stage.alignRNA/star/$meta.assembly.build" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // Assembly report
    withName: 'TOL_SEARCH' {
        executor = 'local'
    }
    withName: 'REPORT_DTOL' {
        executor = 'local'
    }
    withName: 'REPORT_GENOMETRAITS' {
        executor = 'local'
    }
    withName: 'REPORT_SOFTWAREVERSIONS' {
        executor = 'local'
    }
    withName: 'QUARTO' {
        ext.args    = '-P image_path:data'
        ext.prefix  = { "${notebook.baseName}_mqc" }
        stageInMode = 'copy'
        time        = 15.m
    }
    withName: 'MULTIQC' {
        publishDir = [
            path: { "$params.outdir/$stage.report" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
        time       = 30.m
    }

}
