process {

    // PREPARE INPUT
    withName: 'GOAT_TAXONSEARCH' {
        ext.args   = "--lineage --variables odb10_lineage"
    }
    withName: 'PREPARE_INPUT:SAMTOOLS_FASTA' {
        tag        = { "$meta.id:$bam.baseName" }
        ext.prefix = { bam.baseName }
    }

    // BUILD DATABASES
    withName: 'FASTK_FASTK' {
        scratch    = false  // Disable scratch to get around bug https://github.com/nextflow-io/nextflow/issues/2983
        ext.args   = { "-t1 -k${meta.kmer_size}" }
    }
    withName: 'FASTK_MERGE' {
        scratch    = false  // Disable scratch to get around bug https://github.com/nextflow-io/nextflow/issues/2983
        ext.prefix = { "${meta.id}_merged" }
    }
    withName: 'BUILD_HIFI_DATABASES:FASTK_FASTK' {
        ext.prefix = { "${meta.id}_${reads instanceof List ? reads[0].baseName : reads.baseName}_hifi" }
    }
    withName: 'BUILD_HIC_DATABASES:FASTK_FASTK' {
        ext.prefix = { "${meta.id}_${reads instanceof List ? reads[0].baseName : reads.baseName}_hic" }
    }

    // GENOME PROPERTIES
    withName: 'FASTK_HISTEX' {
        ext.args   = "-G"
        ext.prefix = { "${meta.id}_histex" }
    }
    withName: 'GENESCOPEFK' {
        ext.args   = {
            [
                "--kmer_length ${meta.kmer_size}",
                "--ploidy ${meta.ploidy}"
            ].join(' ')
        }
        publishDir = [
            path: { "$params.outdir/01_inspect_data/genescopefk" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }
    withName: 'MERQURYFK_PLOIDYPLOT' {
        publishDir = [
            path: { "$params.outdir/01_inspect_data/ploidyplot" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }
    withName: 'MERQURYFK_KATGC' {
        publishDir = [
            path: { "$params.outdir/01_inspect_data/katgc" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // COMPARE_LIBRARIES
    withName: 'MERQURYFK_KATCOMP' {
        publishDir = [
            path: { "$params.outdir/01_inspect_data/kat_comp" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // SCREEN READS
    withName: 'MASH_SCREEN' {
        ext.args   = '-w'
        ext.prefix = { "${meta.id}_${query.baseName}" }
        publishDir = [
            path: { "$params.outdir/01_inspect_data/mash_screen/screens" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }
    withName: 'MASH_FILTER' {
        ext.args   = "-F '[\\t/]' '\$1 > 0.9 && \$2 > 100'"
        publishDir = [
            path: { "$params.outdir/01_inspect_data/mash_screen" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // HIFIASM
    withName: 'HIFIASM' {
        ext.args   = { meta.settings?.hifiasm ? meta.settings.hifiasm[task.index-1] : "" }
        publishDir = [
            path: { "$params.outdir/03_assembly/hifiasm_${task.index}" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // GFASTATS
    withName: 'GFASTATS' {
        ext.prefix = { assembly.baseName }
        publishDir = [
            path: { "$params.outdir/03_assembly/${meta.build}" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // GFASTATS
    withName: 'GFATOOLS_GFA2FA' {
        ext.prefix = { gfa.baseName }
        publishDir = [
            path: { "$params.outdir/03_assembly/${meta.build}" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // PURGE_DUPLICATES
    withName: 'MINIMAP2_ALIGN_READS' {
        tag        = { meta.build }
        ext.args   = "-x map-hifi"
    }
    withName: 'MINIMAP2_ALIGN_ASSEMBLY_.*' {
        tag        = { meta.build }
        ext.args   = "-x asm5 -DP"
    }
    withName: 'PURGEDUPS_PURGEDUPS' {
        ext.args   = "-2"
    }
    withName: 'PURGEDUPS_CALCUTS' {
        // kmer coverage is very close to read coverage w.r.t long read sequencing.
        // kmer coverage is used as a proxy for read coverage here.
        // kmer coverage is extracted from the GenomeScope model when available.
        ext.args   = { meta.kmercov ? "-m ${meta.kmercov * 1.5} -u ${meta.kmercov * 3}" : "" }
    }
    withName: '(PURGEDUPS|MINIMAP2)_.*_PRIMARY' {
        ext.prefix = { "${meta.id}_${meta.build}_primary" }
    }
    withName: '(PURGEDUPS|MINIMAP2)_.*_ALTERNATE' {
        ext.prefix = { "${meta.id}_${meta.build}_alternate" }
    }
    withName: 'PURGEDUPS_.*' {
        tag        = { meta.build }
        publishDir = [
            path: { "$params.outdir/04_purge/purge_dups" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]      
    }

    // COMPARE_ASSEMBLIES
    withName: 'QUAST' {
        cpus       = { Math.min(6, consensus instanceof List ? consensus.size() : 1 ) }
        publishDir = [
            path: { "$params.outdir/04_purge/quast" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    // EVALUATE_ASSEMBLY
    withName: 'MERQURYFK_MERQURYFK' {
        tag        = { meta.build }
        ext.prefix = { meta.build }
        publishDir = [
            path: { "$params.outdir/04_purge/merquryfk/$meta.build" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: 'BUSCO' {
        tag        = { "${meta.build}-${lineage}" }
        ext.prefix = { "${meta.build}-${lineage}" }
        ext.args   = '--mode genome'
        publishDir = [
            path: { "$params.outdir/04_purge/busco/$meta.build" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
        time       = 4.d
    }

    withName: 'STAR_ALIGN' {
        tag        = { meta.build }
        ext.args   = '--readFilesCommand zcat'
        publishDir = [
            path: { "$params.outdir/rnaseq_alignment/star/$meta.build" },
            mode: params.publish_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

}
